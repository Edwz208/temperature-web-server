#include <WiFi.h>
#include <WiFiClient.h>
#include <WebServer.h>
#include <DHT.h>
#include <FS.h>
#include <SPIFFS.h>
#include <ArduinoJson.h>
#include <time.h>

#define dhtPin 27
#define DHTTYPE DHT11

DHT dht(dhtPin, DHTTYPE);
WebServer server(80);

const char* ssid = "";
const char* password = "";

float temperature = 0;
float humidity = 0;

unsigned long previousMillis = 0;
const long interval = 200;

void setup() {
  Serial.begin(115200);
  dht.begin();
  pinMode(dhtPin, INPUT_PULLUP);
  pinMode(26, OUTPUT); // Initialize LED pin

  if (!SPIFFS.begin(true)) {
    Serial.println("Error with SPIFFS");
    return;
  }

  connectToWifi();
  beginServer();

  configTime(-5 * 3600, 3600, "pool.ntp.org", "time.nist.gov");

  Serial.println("Time syncing...");
  struct tm timeinfo;
  while (!getLocalTime(&timeinfo)) {
    delay(1000);
    Serial.println("...");
  }
  Serial.println("Time synchronized");
}

void loop() {
  server.handleClient();
  
  unsigned long currentMillis = millis();
  if (currentMillis - previousMillis >= interval) {
    previousMillis = currentMillis;
    getTemperature();
    getHumidity();
    struct tm timeinfo;
    char timeStr[20];
    if (getLocalTime(&timeinfo)) {
      strftime(timeStr, sizeof(timeStr), "%H:%M:%S", &timeinfo);
    } else {
      strcpy(timeStr, "00:00:00");
    }
  }
}

void connectToWifi() {
  WiFi.begin(ssid, password);
  
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
}

void beginServer() {
  server.on("/", HTTP_GET, []() {
    serveFile("/index.html", "text/html");
  });
  server.on("/style.css", HTTP_GET, []() {
    serveFile("/style.css", "text/css");
  });
  server.on("/script.js", HTTP_GET, []() {
    serveFile("/script.js", "application/javascript");
  });
  server.on("/data", HTTP_GET, []() { // Handle data request
    struct tm timeinfo;
    char timeStr[20];
    if (getLocalTime(&timeinfo)) {
      strftime(timeStr, sizeof(timeStr), "%H:%M:%S", &timeinfo); 
    } else {
      strcpy(timeStr, "00:00:00");
    }
    handleData(timeStr);
  });
  
  server.onNotFound([]() {
    server.send(404, "text/plain", "File Not Found");
  });

  server.begin();
  Serial.println("HTTP server started");
}

void serveFile(String path, String contentType) {
  if (SPIFFS.exists(path)) {
    File file = SPIFFS.open(path, "r");
    server.streamFile(file, contentType);
    file.close();
  } else {
    server.send(404, "text/plain", "File Not Found");
  }
}

void handleData(String timeStr) { // Handle data and send JSON response
  String json;
  StaticJsonDocument<200> doc;
  doc["temperature"] = temperature;
  doc["humidity"] = humidity;
  doc["time"] = timeStr;
  serializeJson(doc, json);

  server.send(200, "application/json", json);
}

void getTemperature() {
  float temp = dht.readTemperature();
  if (isnan(temp)) {
    Serial.println("Failed to read temperature");
  } else {
    temperature = round(temp * 1000.0) / 1000.0;
    Serial.print("Temperature: ");
    Serial.print(temperature);
    Serial.println(" Â°C");
  }
}

void getHumidity() {
  float hum = dht.readHumidity();
  if (isnan(hum)) {
    Serial.println("Failed to read humidity");
  } else {
    humidity = hum;
    Serial.print("Humidity: ");
    Serial.print(humidity);
    Serial.println(" %");
  }
}
